MODULE main
	VAR
		flag : array 0..1 of boolean;
		turn : {0,1};
		p0 : process proc(0, turn, flag);
		p1 : process proc(1, turn, flag);

	ASSIGN
		init(flag[0]) := FALSE;
		init(flag[1]) := FALSE;
		init(turn) := 0;

	--- safety
--	SPEC AG !( (p0.state = c) & (p1.state = c) )

	--- liveness
	SPEC AG ( (p0.state = t) -> AF (p0.state = c) )
--	SPEC AG ( (p1.state = t) -> AF (p1.state = c) )

MODULE proc(id, turn, flag)
	VAR
		line : {0, 1, 2, 3, 4, 5, 6};
		state : {n, t, c};

	ASSIGN
		init(line) := 0;
		next(line) :=
			case
				(line = 1) & (turn = id) : 4;
				(line = 2) & (flag[1 - id]) : 2;
				(line = 3) : 1;	-- loop-back
				TRUE : (line + 1) mod 7;
			esac;

		init(state) := n;
		next(state) :=
			case
				(line = 0) : t;
				(line = 4) : c;
				(line = 6) : n;
				TRUE : state;
			esac;

		next(flag[id]) :=
			case
				(line = 0) : TRUE;
				(line = 5) : FALSE;
				TRUE : flag[id];
			esac;

		next(turn) :=
			case
				(line = 3) : id;
				TRUE : turn;
			esac;

	FAIRNESS running;